/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// configuration of the Maven artifacts
apply plugin: 'maven-publish'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                def Node root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.projectDescription)
                root.appendNode('url', project.projectUrl)

                def issues = root.appendNode( 'issueManagement' )
                issues.appendNode( 'system', 'github' )
                issues.appendNode( 'url', project.projectIssuesUrl )

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', project.projectScm )
                scm.appendNode( 'connection', project.projectConnection)
                scm.appendNode( 'developerConnection', project.projectDevConnection )

                def license = root.appendNode( 'licenses' ).appendNode( 'license' );
                license.appendNode( 'name', 'The Apache Software License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt' )
                license.appendNode( 'distribution', 'repo' )

                def dev = root.appendNode( 'developers' ).appendNode( 'developer' );
                dev.appendNode( 'id', project.projectVendor )
                dev.appendNode( 'name', project.projectVendorName )
                dev.appendNode( 'organization', 'Grails' )
                dev.appendNode( 'organizationUrl', 'http://grails.org' )
            }
        }
    }
}